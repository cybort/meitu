apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.objectbox' // after applying Android plugin

def keystoreFile = rootProject.file("keystore.properties")
def keystore = new Properties()
keystore.load(new FileInputStream(keystoreFile))


android {
    compileSdkVersion android_compile_sdk
    buildToolsVersion android_build_tools
    defaultConfig {
        minSdkVersion android_min_sdk
        targetSdkVersion android_target_sdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "fav_model", "true"
        buildConfigField "boolean", "fav_organ", "true"
    }
    signingConfigs {
        release {
            storeFile file(keystore['RELEASE_STORE_FILE'])
            keyAlias keystore['RELEASE_KEY_ALIAS']
            storePassword keystore['RELEASE_KEY_PASSWORD']
            keyPassword keystore['RELEASE_STORE_PASSWORD']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    flavorDimensions 'meitu'
    productFlavors {
        aitaotu {
            dimension 'meitu'
            applicationId 'io.github.yueeng.aitaotu'
            resValue "string", "app_name", "爱套图"
            resValue "string", "app_search_provider", "${applicationId}.SearchHistoryProvider"
            buildConfigField "boolean", "fav_model", "false"
            buildConfigField "boolean", "fav_organ", "false"
        }
        meituri {
            dimension 'meitu'
            applicationId 'io.github.yueeng.meituri'
            resValue "string", "app_name", "美图日"
            resValue "string", "app_search_provider", "${applicationId}.SearchHistoryProvider"
        }
        meitulu {
            dimension 'meitu'
            applicationId 'io.github.yueeng.meitulu'
            resValue "string", "app_name", "美图录"
            resValue "string", "app_search_provider", "${applicationId}.SearchHistoryProvider"
        }
    }
    def apks = []
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('release.apk')) {
                def fileName = "app-${variant.productFlavors[0].name}-release.apk"
                outputFileName = fileName
                apks.add(outputFile.parentFile.absolutePath + File.separator + fileName)
            }
        }
    }
    task publish(dependsOn: 'assembleRelease') {
        doLast {
            def folder = rootDir.getAbsolutePath() + File.separator + "apk"
            delete fileTree(dir: folder, include: "**/*.apk")
            apks.forEach { apk ->
                copy {
                    from apk
                    into folder
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:support-annotations:$android_support_version"
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-android-support:$okhttp_version"
    implementation 'org.jetbrains.anko:anko-sdk15:0.10.3'
    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    implementation "io.objectbox:objectbox-android:$objectboxVersion"
    implementation "io.objectbox:objectbox-kotlin:$objectboxVersion"
    kapt "io.objectbox:objectbox-processor:$objectboxVersion"
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    implementation 'io.objectbox:objectbox-rxjava:0.9.8'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    kapt 'com.github.bumptech.glide:compiler:4.3.1'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.3.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.8.0'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}